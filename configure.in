dnl Process this file with autoconf to produce a configure script.
##
## This file is part of jwhois
## Copyright (C) 1999  Jonas Öberg
##
## This program is free software; you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation; either version 2 of the License, or
## (at your option) any later version.
##
## This program is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with this program; if not, write to the Free Software
## Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
##
AC_INIT(src/jconfig.c)
AM_INIT_AUTOMAKE(jwhois,2.3pre1)
AM_CONFIG_HEADER(config.h)

AC_ARG_ENABLE(DEFAULTHOST,
[  --enable-DEFAULTHOST=HOST   default whois host (default whois.internic.net)],
[AC_DEFINE_UNQUOTED(DEFAULTHOST, "$enable_DEFAULTHOST")],
[AC_DEFINE(DEFAULTHOST, "whois.internic.net")])

AC_ARG_WITH(cache,
[  --without-cache             disable cache manager],
[if test x$withval = xyes; then
  AC_DEFINE(CACHE,1)
  cache=yes
 elif test x$withval = xno; then
  cache=no
 fi],
[AC_DEFINE(CACHE,1)
 cache=yes])

AC_PROG_INSTALL
AC_PROG_CC
AM_PROG_LEX

AC_CHECK_FUNC(connect,,
  AC_CHECK_LIB(socket, connect))
AC_CHECK_FUNC(socket,,
  AC_CHECK_LIB(socket, socket)
  AC_CHECK_LIB(inet, socket))
AC_CHECK_FUNC(getaddrinfo,
  AC_DEFINE(HAVE_GETADDRINFO,1)
  AC_CHECK_FUNCS(gai_strerror),
  AC_CHECK_FUNC(gethostbyname,,
    AC_CHECK_LIB(nsl, gethostbyname))
  AC_CHECK_FUNC(inet_aton))

AC_CHECK_LIB(inet6, main,
  AC_CHECK_LIB(resolv, main))

AC_CHECK_FUNCS(memcpy strtol)
AC_CHECK_FUNCS(strcasecmp strncasecmp getopt_long)
AC_STDC_HEADERS
AC_CHECK_HEADERS(sys/types.h sys/socket.h netinet/in.h netdb.h)

if test x$cache = xyes; then
  AC_CHECK_FUNC(dbm_open, AC_DEFINE(HAVE_DBM_OPEN),
    AC_CHECK_LIB(gdbm, gdbm_open, AC_DEFINE(HAVE_GDBM_OPEN),
      AC_CHECK_LIB(ndbm, dbm_open, AC_DEFINE(HAVE_DBM_OPEN))))
  AC_CHECK_HEADERS(gdbm.h ndbm.h dbm.h)
fi

AC_OUTPUT(Makefile doc/Makefile src/Makefile)
